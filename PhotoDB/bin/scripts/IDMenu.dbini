<#>[]{scripts\dbe.dbini}
<#>[]{scripts\Actions.dbini}

<Find file manually>[DB_IC_SEARCH]
{
initialization:
  if ($StaticImage=false) {InVisible;};
  if ($IsFile=true) {InVisible;};
run:
 $mask=GetImagesMask;
 $caption=T("Find this file manually");
 $File=GetOpenImageFileName($caption,$mask);
 if ($File!="")
 {
  SetFileNameByID($ID,$File);
 };
}

<Slide Show>[DB_IC_SLIDE_SHOW]
{
initialization:
  Default;
run:
 ShowItemPopUpMenu; 
}

<Compare>[DB_IC_SLIDE_SHOW]
{
initialization:
 $skd=ShiftKeyDown;
 if ($skd=false) {InVisible;};
 if ($IsFile=false) {InVisible;};
 $SelCount=0;
 if ($Visible=true) {
 for($i=0;$i<$MenuLength;$i=%$i+1)
 {
  LoadVariablesSelectedFileNo($i);
  if ($Selected=true) 
  {
   $SelCount=%$SelCount+1;
   if ($SelCount=1) {$f2=$FileName;};
   if ($SelCount=2) {$f1=$FileName;};
  };
 };
 LoadVariablesSelectedFileNo($Position);
 if ($SelCount!=2) {InVisible;};
 };
run:
 
 $cp=Compare2Images($f1,$f2,60);
 $s=IntToStr($cp);
 $user=GetCurrentUser;
 $nl=Char(13);
 $text=T("Dear {2}!{1}The degree of similarity of these 2 images is - {3}%!{1}Good luck!");
 $s=Format($text,$nl,$user,$s);
 $p=%$MB_OK+$MB_ICONINFORMATION;
 $caption=T("Analyzer");
 MessageBox($s,$caption,$p);
}

<Apply actions>[DB_IC_APPLY_ACTION]
{
initialization:
  if ($FileExists=false) {InVisible;};  
  $sID=REGISTER_SCRIPT;
  $script="$file=GetOpenFileName(""Result.dbact"",""DB Action Files (*.dbact)|*.dbact"");ApplyActionsToSelected("""+$sID+""",$file);";
  CreateItem("Select",DB_IC_APPLY_ACTION,$script,false,0);
  CreateItem("-",DB_IC_APPLY_ACTION,"",false,0);

  $DBProgramDir=GetProgramFolder;
  $ActionsDir=$DBProgramDir+"Actions";
  $files=GetDirListing($ActionsDir,"|DBACT|");

  $FilesLength=ArrayStringLength($files);
  for($i=0;$i<$FilesLength;$i=%$i+1)
  { 
     $file=GetStringItem($files,$i);

     $file_name=ExtractFileName($file);

     $script="ApplyActionsToSelected("""+$sID+""","""+$file+""");";
     CreateItem($file_name,DB_IC_APPLY_ACTION,$script,false,0);
  };

   run:

//$file=GetOpenFileName("Result.dbact","DB Action Files (*.dbact)|*.dbact");
//$sID=REGISTER_SCRIPT;
//ApplyActionsToSelected($sID,$file);
}

<Shell>[DB_IC_SHELL]
{
initialization:
 $skd=ShiftKeyDown;
 if ($skd=false) {InVisible;};
 if ($FileExists=false) {InVisible;};
run:
  ShellExecutePopUpMenu;
}

<Links>[DB_IC_NOTES]
{
initialization:
 $AllLinks=SplitLinks($Links,"");
 $L=ArrayStringLength($AllLinks);
 if ($L=0) 
 {
  InVisible;
 }; else 
 {
 $skd=ShiftKeyDown;
 if ($skd=true)
 {
   for ($i=0;$i<$L;$i=SumInt($i,1)) 
   {
     $link=GetStringItem($AllLinks,$i);
     $aCaption=LinkName($link);
     $Type=LinkType($link);
     $Value=LinkValue($link);
     $Icon=-1;
     $RunScript="";
     
     if ($Type=$LINK_TYPE_ID) {$ID=StrToInt($Value); $FaileName=GetFileNameByID($ID); $Icon=DB_IC_SLIDE_SHOW;};
     if ($Type=$LINK_TYPE_ID_EXT) {$IDEx=DeCodeExtID($Value); $aFileName=GetFileNameByIDEx($IDEx); $Icon=DB_IC_NOTES;};
     if ($Type=$LINK_TYPE_IMAGE) {$aFileName=$Value; $Icon=DB_IC_DESKTOP;};
     if ($Type=$LINK_TYPE_FILE) {$aFileName=$Value; $Icon=DB_IC_SHELL;};
     if ($Type=$LINK_TYPE_FOLDER) {$aFileName=$Value; $Icon=DB_IC_DIRECTORY;};
     if ($Type=$LINK_TYPE_TXT) {$aFileName="::"; $Icon=DB_IC_TEXT_FILE;};
     
     $fe=FileExists($aFileName);
     if ($fe=true) 
     {
       $im=ImageFile($aFileName);
       if ($im=true) 
       {
         
         $RunScript=PathFormat("ShowFile(""%1"");",$aFileName);
       }; else 
       {
         $RunScript=PathFormat("Run(""%1"","""");",$aFileName);
       };
     }; else 
     {
       $de = DirectoryExists($aFileName);
       if ($de=true) 
       {
         $RunScript=PathFormat("NewExplorerByPath(""%1"");",$aFileName);
       };
     };
     if ($aFileName="::") 
     {
       $RunScript="Hint(""%1"","""+$Value+""");";
       $RunScript=PathFormat($RunScript,$aCaption);
     };
     CreateItem($aCaption,$Icon,$RunScript,false,0);
    };
   }; else { InVisible; };
 };
}

<Refresh>[DB_IC_REFRESH_THUM]
{
initialization:
  if ($NoDBInfoNeeded=true) {InVisible;};
run:
  RefreshThumItemPopUpMenu;
}

<Rotate>[DB_IC_ROTETED_0]{
initialization:
  if ($IsRecord=false) {InVisible;};
  if ($NoDBInfoNeeded=true) {InVisible;};

  for ($i=0;$i<4;$i=SumInt($i,1)) {
      if ($i=0) {$s="No rotate";};
      if ($i=1) {$s="Rotate right";};
      if ($i=2) {$s="Rotate 180°";};
      if ($i=3) {$s="Rotate left";};
      $n=SumInt($i,DB_IC_ROTETED_0);
      if ($Rotation=$i) {
      CreateItem($s,$n,"SetRotateItemPopUpMenu;",true,$i);
      }; else {
      CreateItem($s,$n,"SetRotateItemPopUpMenu;",false,$i);
      };
   };
}

<Rating>[DB_IC_RATING_STAR]{
initialization:
  if ($IsRecord=false) {InVisible;};
  if ($NoDBInfoNeeded=true) {InVisible;};
   CreateItem("0",4,"SetRatingItemPopUpMenu;",false,0);
   for ($i=1;$i<6;$i=SumInt($i,1)) {
     $s=IntToStr($i);
     $n=SumInt(16,$i);
     if ($Rating=$i) {
     CreateItem($s,$n,"SetRatingItemPopUpMenu;",true,$n); }; else {
     CreateItem($s,$n,"SetRatingItemPopUpMenu;",false,$n);
     };
   };
}

<Private>[DB_IC_PRIVATE]
{ 
 initialization:
  if ($IsRecord=false) {InVisible;};
  if ($NoDBInfoNeeded=true) {InVisible;};
  if ($Access=1) {InVisible;};
  $Tag=0;
run:
  PrivateItemPopUpMenu;
}

<Public>[DB_IC_COMMON]
{
 initialization:
  if ($IsRecord=false) {InVisible;};
  if ($NoDBInfoNeeded=true) {InVisible;};
  if ($Access=0) {InVisible;};
  $Tag=1;
run:
  PrivateItemPopUpMenu;
}

<Date>[DB_IC_EDIT_DATE]
{
 initialization:
  if ($IsRecord=false) {InVisible;};
  if ($NoDBInfoNeeded=true) {InVisible;};
  Tag(1);
 run:
  DateItemPopUpMenu;
}

<Groups>[DB_IC_GROUPS]
{ 
 initialization:
 if ($IsRecord=false) {InVisible;};
 if ($NoDBInfoNeeded=true) {InVisible;};
 $len=ArrayStringLength($GroupsCodes);

  for ($i=0;$i<$len;$i=SumInt($i,1)) {
      $s=GetStringItem($GroupsCodes,$i);
      $caption=GetStringItem($GroupsNames,$i);
      $n=GetGroupImage($s);
      CreateItem($caption,$n,"QuickGroupInfoPopUpMenu;",false,0);
   };
  CreateItem("-",0,"",false,0);

  CreateItem("Edit groups",DB_IC_GROUPS,"GroupsPopUpMenu;",false,0);

}

<Keywords>[DB_IC_NOTEPAD]
{ 
initialization:
  $KeyWordsArray=SplitWords($KeyWords);
  $len=ArrayStringLength($KeyWordsArray);
  for ($i=0;$i<$len;$i=SumInt($i,1)) {
      $caption=GetStringItem($KeyWordsArray,$i);
      CreateItem($caption,DB_IC_NOTEPAD,"ShowKeyWord($Caption);",false,0);
   }; 
  if ($len=0) {InVisible;}; 
  $shift=ShiftKeyDown;
  if ($shift=true) {InVisible;}; 
}

<Encrypting>[DB_IC_KEY]
{
initialization:
  if ($Crypted=true) {
    CreateItem("Enter password",DB_IC_PASSWORD,"EnterPasswordItemPopUpMenu;",false,0);
    CreateItem("Decrypt",DB_IC_DECRYPTIMAGE,"DeCryptItemPopUpMenu;",false,0);
  }; else {
    CreateItem("Encrypt",DB_IC_CRYPTIMAGE,"CryptItemPopUpMenu;",false,0);
  };
  CreateItem("-",0,"",false,0);
  $script = "DoSteno("""+$FileName+""")";
  CreateItem("Hide info in image",DB_IC_STENO,$script,false,0);
  $can_desteno=FileHasExt($FileName,"|BMP|PNG|");
  if ($can_desteno=true) {
    $script = "DoDesteno("""+$FileName+""")";
    CreateItem("Extract info from image",DB_IC_DESTENO,$script,false,0);
  };
}

<Set as desktop wallpaper>[DB_IC_WALLPAPER]
{
initialization:
  if ($IsCurrentFile=false) {InVisible;};
  if ($WallPaper=false) {InVisible;};
  if ($Crypted=true) {InVisible;};
  CreateItem("Stretch",DB_IC_WALLPAPER,"WallpaperStretchItemPopUpMenu;",false,0);
  CreateItem("Center",DB_IC_WALLPAPER,"WallpaperCenterItemPopUpMenu;",false,0);
  CreateItem("Tile",DB_IC_WALLPAPER,"WallpaperTileItemPopUpMenu;",false,0);
}

<Image editor>[DB_IC_IMEDITOR]
{
initialization:
  if ($IsCurrentFile=false) {InVisible;};
run:
  ImageEditorItemPopUpMenu;
}

<Convert image>[DB_IC_RESIZE]
{
initialization:
  if ($IsCurrentFile=false) {InVisible;};
run:
  ConvertItemPopUpMenu;
}

<>[]{scripts\UserMenu.dbini}

<Update info>[DB_IC_REFRESH_ID]
{
initialization:
  if ($NoDBInfoNeeded=true) {InVisible;};
  if ($IsCurrentFile=false) {InVisible;};
  if ($IsRecord=false) {InVisible;};
run:
  RefreshIDItemPopUpMenu;
}

<Scan image>[DB_IC_SEARCH]
{
initialization:
  $shift=ShiftKeyDown;
  if ($shift=true) {InVisible;};
  if ($FileExists=false) {InVisible;};
run: 
  ScanImageItemPopUpMenu;
}

<Print>[DB_IC_PRINTER]
{
initialization:
  if ($IsCurrentFile=false) {InVisible;};
run: 
  PrintItemPopUpMenu;
}

<Duplicates>[DB_IC_DUBLICAT]
{
initialization:
  if ($Dublicat=false) {InVisible;};
  CreateItem("Show duplicates",DB_IC_DUBLICAT,"ShowDublicatesItemPopUpMenu;",false,0);
  CreateItem("Delete duplicates",DB_IC_DEL_DUBLICAT,"DeleteDublicatesItemPopUpMenu;",false,0);
}

<Delete>[DB_IC_DELETE_FILE]
{
initialization:
  if ($NoDBInfoNeeded=true) {InVisible;};
  if ($IsRecord=false) {InVisible;};
  
  CreateItem("Delete info from collection",DB_IC_DELETE_FILE,"DeleteLItemPopUpMenu;",false,0);

  if ($IsFile=true) {
    if ($FileExists=true)
	{
	  CreateItem("Delete file",DB_IC_DELETE_FILE,"DeleteItemPopUpMenu;",false,0);
	};
  };
}

<Copy>[DB_IC_COPY]
{
initialization:
  if ($IsFile=false) {InVisible;};
  if ($FileExists=false) {InVisible;};
run:
  CopyItemPopUpMenu;
}

<Rename>[DB_IC_RENAME]
{
  RenameItemPopUpMenu;
}

<->[]{}

<Open in Explorer>[DB_IC_EXPLORER]
{
initialization:
 if ($FileExists=false) {InVisible;};
run:
  ExplorerPopUpMenu;
}

<Send to>[DB_IC_SENDTO]
{
  initialization:
  $len=ArrayStringLength($Panels);
  for ($i=0;$i<$len;$i=SumInt($i,1)) {
      $s=GetStringItem($Panels,$i);
      CreateItem($s,DB_IC_SEND,"SendToItemPopUpMenu;",false,$i);
   };
  CreateItem("-","","",false,0);
  CreateItem("New Panel",DB_IC_SENDTO,"SendToItemPopUpMenu;",false,-1);
}

<Show folder>[DB_IC_SEARCH]
{
initialization:
  if ($IsRecord=false) {InVisible;};
  if ($NoDBInfoNeeded=true) {InVisible;};
run:
  SearchFolderPopUpMenu;
}

<Properties>[DB_IC_PROPERTIES]
{
  PropertyItemPopUpMenu;
}